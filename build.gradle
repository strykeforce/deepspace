plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id "edu.wpi.first.GradleRIO" version "2019.2.1"
    id "idea"
    id "com.diffplug.gradle.spotless" version "3.16.0"
}

def ROBOT_MAIN_CLASS = "frc.team2767.deepspace.Main"

deploy {
    targets {
        roboRIO("roborio") {
            team = frc.getTeamNumber() // loaded from .wpilib/wpilib_preferences.json
        }
    }
    artifacts {
        frcJavaArtifact("frcJava") {
            targets << "roborio"
            debug = frc.getDebugOrDefault(false)
        }
        // Built in artifact to deploy arbitrary files to the roboRIO.
        fileTreeArtifact("frcStaticFileDeploy") {
            // The directory below is the local directory to deploy
            files = fileTree(dir: "src/main/deploy")
            // Deploy to RoboRIO target, into /home/lvuser/deploy
            targets << "roborio"
            directory = "/home/lvuser/deploy"
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile wpi.deps.wpilib()
    compile wpi.deps.vendor.java()
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)
    compile "org.strykeforce.thirdcoast:swerve:${thirdcoastVersion}"
    compile "org.strykeforce.thirdcoast:telemetry:${thirdcoastVersion}"
    compile "ch.qos.logback:logback-classic:${logbackVersion}"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"

    // Testing
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testCompile "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testCompile "org.assertj:assertj-core:3.11.1"

}

compileKotlin{
    kotlinOptions{
        jvmTarget = "1.8"
        freeCompilerArgs = ["-progressive"]
    }
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}


spotless {
  java {
    googleJavaFormat()
    lineEndings = 'GIT_ATTRIBUTES'
  }
}

test {
    useJUnitPlatform()
}
